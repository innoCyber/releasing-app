apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {
    signingConfigs {
        Release {
            storeFile file('G:\\Android\\GrayStatic\\ReleasingNew\\keystore.jks')
            storePassword 'grimaldi'
            keyAlias = 'grimaldi'
            keyPassword 'grimaldi'
        }
    }
    compileSdkVersion rootProject.compileSdkVersion
    defaultConfig {
        applicationId "ptml.releasing"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 4
        versionName "1.1.0"
        testInstrumentationRunner "ptml.releasing.ReleasingTestRunner"
        vectorDrawables.useSupportLibrary true
        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions 'server'
    productFlavors {
        dev {
            dimension 'server'
            applicationIdSuffix '.dev'
            versionCode 1
            signingConfig signingConfigs.Release
        }

        staging {
            dimension 'server'
            applicationIdSuffix '.staging'
            versionCode 6
        }

        production {
            dimension 'server'
            signingConfig signingConfigs.Release
        }
    }

    applicationVariants.all { variant ->
        def name = variant.getName()
        //default values
        variant.buildConfigField("String", "IMEI", "\"861327032935756\"")

        // Specific values
        if (name.contains("dev")) {
            variant.buildConfigField("String", "BASE_URL", "\"http://192.168.43.115:3000\"")
        } else if (name.contains("staging")) {
            variant.buildConfigField("String", "BASE_URL", "\"https://billing.grimaldi-nigeria.com:1448/api/AndroidZebra/\"")
        } else if (name.contains("production")) {
            variant.buildConfigField("String", "BASE_URL", "\"https://billing.grimaldi-nigeria.com:1449/api/AndroidZebra/\"")
            variant.buildConfigField("String", "IMEI", "\"\"")
        }
    }

    dataBinding {
        enabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    //kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine"

    //Android Kotlin extensions
    implementation 'androidx.core:core-ktx:1.0.1'

    //appcompat
    implementation "androidx.appcompat:appcompat:$supportLibVersion"
    implementation "androidx.vectordrawable:vectordrawable:$supportLibVersion"

    //multidex
    implementation 'androidx.multidex:multidex:2.0.1'

    //constraint
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"

    //material
    implementation "com.google.android.material:material:$materialLibVersion"

    // architecture components
    implementation "androidx.lifecycle:lifecycle-extensions:$archComponentsVersion"
    implementation "androidx.lifecycle:lifecycle-runtime:$archComponentsVersion"
    kapt "androidx.lifecycle:lifecycle-compiler:$archComponentsVersion"

    // retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpLoggingInterceptorVersion"
//    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:$retrofitCoroutine"

    //dagger
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"
    compileOnly 'javax.annotation:jsr250-api:1.0'

    //timber for logging
    implementation "com.jakewharton.timber:timber:$timberVersion"

    //leakcanary for detecting memory leaks
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    //permissions
    implementation "org.permissionsdispatcher:permissionsdispatcher:$permission_dispatcher"
    kapt "org.permissionsdispatcher:permissionsdispatcher-processor:$permission_dispatcher"

    //secure shared preferences
    implementation 'com.scottyab:secure-preferences-lib:0.1.7'

    // unit testing
    testImplementation "junit:junit:$junitVersion"
    testImplementation 'android.arch.core:core-testing:1.1.1'
    testImplementation "androidx.test:core:${coreTestVersion}"
    testImplementation "io.mockk:mockk:$mockkVersion"
    testImplementation "org.robolectric:robolectric:${robolectricVersion}"
    testImplementation "com.google.truth:truth:${truthVersion}"

    //instrumentation testing
    androidTestImplementation "androidx.test:runner:$runnerVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test:runner:$testRunner"
    androidTestImplementation "androidx.test:rules:$testRunner"
    androidTestImplementation "androidx.test.ext:junit:$androidJunit"

    //zebra sdk
    implementation files('libs/zebra_android_sdk.jar')

    //barcode scanning
    implementation 'me.dm7.barcodescanner:zbar:1.8.3'

    //crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    //firebase analytics
    implementation 'com.google.firebase:firebase-core:17.0.0'

    //firebase remote config
    implementation 'com.google.firebase:firebase-config:18.0.0'


    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    implementation "androidx.paging:paging-runtime:$pagingVersion"

    debugImplementation "com.amitshekhar.android:debug-db:1.0.6"

}

apply plugin: 'com.google.gms.google-services'
